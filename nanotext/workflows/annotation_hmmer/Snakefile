shell.executable('/bin/bash')


IDS, = glob_wildcards(config['genomes'] + '{sample}.fa')  # .gz
outdir = config['outdir'] + 'data/'
logdir = config['outdir'] + 'log/'


rule all:
    input:
        expand(outdir + '{sample}/orfs.domtbl.tsv', sample=IDS),


# rule unzip:
#     input:
#         config['genomes'] + '{id}_genomic.fna.gz'
#     output:
#         config['genomes'] + '{id}_genomic.fna'
#     shell:
#         '''
#         gunzip {input}
#         '''


rule call_orfs:
    input:
        config['genomes'] + '{id}.fa'
    output:
        outdir + '{id}/orfs.fa'
    log:
        logdir + 'prodigal/{id}.log'
    shell:
        '''
        prodigal -i {input} -f gff -a {output} > /dev/null 2> {log}
        '''


rule hmm:
    input:
        db = config['database'],
        orfs = outdir + '{id}/orfs.fa'
    output:
        outdir + '{id}/orfs.domtbl'
    threads:
        8  # HMMER is not really using all cores
    log:
        logdir + 'hmm/{id}.log'
    shell:
        '''
        hmmscan --cut_ga --noali --cpu {threads} --domtblout {output} \
            {input.db} {input.orfs} \
            > {log} 2>&1
        '''


rule hmm_reformat:
    '''
    Thresholds for bacterial protein domains were set according to
    recommendations from dbCAN (v6), see "readme.txt" under

    http://csbl.bmb.uga.edu/dbCAN/download.php
    
    This approach has also been taken by:

    Stewart, Rob D., Marc Auffret, Rainer Roehe, and Mick Watson. 2018. â€œOpen
    Prediction of Polysaccharide Utilisation Loci (PUL) in 5414 Public
    Bacteroidetes Genomes Using PULpy.â€ bioRxiv.
    https://doi.org/10.1101/421024.
    '''
    input:
        outdir + '{id}/orfs.domtbl'
    output:
        outdir + '{id}/orfs.domtbl.tsv'
    params:
        coverage = 0.35,
        evalue = 1e-18,
    shell:
        '''
        python scripts/HmmPy.py \
            --cov {params.coverage} --evalue {params.evalue} {input} > {output}
        '''



